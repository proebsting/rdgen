<< from grammar import Production,Alts,Seq,Sym,Opt,Rep,Expr,Parens,Lambda,Cons,Spec >>

spec:
    { code }'preamble
    grammar'g
    = << Spec(preamble,g) >>
    .

grammar:
    production'p 
    { production }'L 
    = << [p]+L >> 
    .

production: 
    id'lhs 
    ":" 
    alternation'rhs 
    "." 
    = << Production(lhs, rhs) >> 
    .

alternation: 
    sequence'x 
    { "|" @sequence }'L
    =<< Alts([x]+L) if L else x >>
    .

sequence: 
    { code }'prologue 
    term't
    << ret = last = Cons(t, Lambda()) >>
    << ret.prologue = prologue >>
    {
        term't              
        << last.cdr = Cons(t, last.cdr) >> 
        << last = last.cdr              >> 
    } !
    [ "=" @code ]'c       << ret.code = c or None >>
    = <<ret>> 
    .

term: 
    [ "@" ]     'at 
    @base        't 
    [ "!" ]     'simple
    [ "'" @id ] 'name 
    { code }    'stmts
    << t.keep   = at is not None       >>
    << t.simple = simple is not None   >>
    << t.name   = name or None         >>
    << t.stmts  = stmts                >>
    .

base: 
    "(" alternation'v ")"     =<< Parens(v)  >>
    | "{" alternation'v "}"   =<< Rep(v)     >>
    | "[" alternation'v "]"   =<< Opt(v)     >>
    | id'id                   =<< Sym(id)    >>
    | str's                   =<< Sym(s)     >>
    .

code: CODE'c   =<< c.value.strip() >> .
id  : ID'id    =<< id.value        >> .
str : STR'id   =<< id.value        >> .