<< from grammar import Production,Alts,Sym,Opt,Rep,Parens,Lambda,Cons,Spec,Break,Expr,Continue,OnePlus,Sequence >>

%% local.spec.preamble = "list[str]"
spec:
    { code }'preamble
    grammar'g
    = << Spec(preamble,g) >>
    .

%% local.grammar.prods = "list[Production]"
grammar:
    production'p 
    { production }'prods
    = << [p]+prods >> 
    .

production: 
    id'lhs 
    ":" 
    alternation'rhs 
    "." 
    = << Production(lhs, rhs) >> 
    .

%% local.alternation.seqs = "list[Sequence]"
alternation: 
    sequence'x 
    { "|" @sequence }'seqs
    =<< Alts([x]+seqs) if seqs else x >>
    .

%% local.sequence.prologue = "list[str]"
sequence: 
    { code }'prologue 
    term't
    << ret = last = Cons(t, Lambda()) >>
    {
        term't              
        << last.cdr = Cons(t, last.cdr) >> 
        << last = last.cdr              >> 
    } !
    [ "=" @code ]'c
    = << Sequence(prologue, ret, c) >> 
    .

%% local.term.stmts = "list[str]"
term: 
    [ "@" ]     'at 
    @base       't 
    [ "!" ]     'simple
    [ "'" @id ] 'name 
    { code }    'stmts
    << t.keep   = at is not None       >>
    << t.simple = simple is not None   >>
    << t.name   = name or None         >>
    << t.stmts  = stmts                >>
    .

base: 
    "(" alternation'v ")"     =<< Parens(v)  >>
    | "{" alternation'v "}"   =<< Rep(v)     >>
    | "[" alternation'v "]"   =<< Opt(v)     >>
    | "{+" alternation'v "+}" =<< OnePlus(v) >>
    | id'id                   =<< Sym(id)    >>
    | str's                   =<< Sym(s)     >>
    | "break"                 =<< Break()    >>
    | "continue"              =<< Continue() >>
    .

code: CODE'c   =<< c.value.strip() >> .
id  : ID'id    =<< id.value        >> .
str : STR'id   =<< id.value        >> .

%% [nonterm]
%% str = "str"
%% id  = "str"
%% code = "str"
%% base = "Expr"
%% term = "Expr"
%% sequence = "Sequence"
%% alternation = "Alts | Sequence"
%% production = "Production"
%% grammar = "list[Production]"
%% spec = "Spec"
